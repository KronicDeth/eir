//-*- mode: rust -*-

use libeir_diagnostics::{ByteIndex, ByteSpan, Diagnostic, Label as DiagLabel};
use libeir_intern::{Ident, Symbol};
use libeir_util_parse::ErrorReceiver;
use libeir_util_number::ToPrimitive;

use crate::{BasicType, BinOp};
use crate::constant::Integer;
use crate::text::parser::lexer::Token;
use crate::text::ast::{Module, ModuleItem, Function, FunctionItem, Label,
                       Op, CallControlFlowOp, CallFunctionOp, Value,
                       Assignment, UnpackValueListOp, IfBoolOp,
                       TraceCaptureRawOp, MatchEntry, MatchKind,
                       MatchOp, CaseOp, CaseEntry, CasePattern, DynOpt};
use super::ParserErrorReceiver;
use super::errors::{ParserError, Errors};

grammar<'a>(errors: &'a mut ParserErrorReceiver<'a>);

#[inline]
Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => {
        let mut v = v;
        if let Some(n) = e {
            v.push(n);
        }
        v
    }
};

pub Module: Module = {
    <name:atom> "{" <items:ModuleItem*> "}" => {
        Module {
            name: name,
            items,
        }
    }
};

ModuleItem: ModuleItem = {
    <Function> => ModuleItem::Function(<>),
};

pub StandaloneFunction: (Ident, Function) = {
    <module:atom> ":" <name:atom> "/" <arity:integer> "{" <items:FunctionItem*> "}" => {
        (
            module,
            Function {
                name: name,
                arity: arity,
                items,
            }
        )
    }
};

Function: Function = {
    <name:atom> "/" <arity:integer> "{" <items:FunctionItem*> "}" => {
        Function {
            name: name,
            arity: arity,
            items,
        }
    }
};

FunctionItem: FunctionItem = {

    // Assignments
    <FunctionAssignItem> ";" => <>,

    // Operations
    <FunctionOp> ";" => FunctionItem::Op(<>),

    // Label
    <l:@L> <name:Value> "(" <args:Comma<Value>> ")" ":" <r:@R> => {
        let span = ByteSpan::new(l, r);
        if name.block().is_none() {
            errors.error(
                Diagnostic::new_error("label name must be block")
                    .with_label(DiagLabel::new_primary(span))
                    .into()
            );
        }
        for arg in args.iter() {
            if arg.value().is_none() {
                errors.error(
                    Diagnostic::new_error("argument to label must be value")
                        .with_label(DiagLabel::new_primary(span))
                        .into()
                );
            }
        }
        FunctionItem::Label(Label { name: name, args })
    },

};

#[inline]
FunctionAssignItem: FunctionItem = {
    <l:@L> <lhs:Value> "=" <rhs:Value> <r:@R> => {
        if lhs.value().is_none() {
            errors.error(
                Diagnostic::new_error("assignment must be to a value")
                    .with_label(DiagLabel::new_primary(ByteSpan::new(l, r)))
                    .into()
            );
        }

        FunctionItem::Assignment(Assignment {
            lhs,
            rhs,
        })
    }
};

DynOpt: DynOpt = {
    "(" <entries:Comma<DynOpt>> ")" => DynOpt::Parens(entries),
    <value:Value> => DynOpt::Value(value),
};

DynOpts: Vec<DynOpt> = {
    <opts:DynOpt*> => opts,
};

#[inline]
FunctionOp: Op = {

    // Intrinsic/Dynop
    "@" <name:ident> <opts:DynOpts> => {
        Op::Dyn(name, opts)
    },

    // Call
    <target:Value> "(" <args:Comma<Value>> ")" "=>" <ret:Value> "except" <thr:Value> => {
        Op::CallFunction(CallFunctionOp {
            target,
            ret,
            thr,
            args,
        })
    },
    <target:Value> "(" <args:Comma<Value>> ")" => {
        Op::CallControlFlow(CallControlFlowOp {
            target,
            args,
        })
    },

    // UnpackValueList
    "unpack" <value:Value> "arity" <arity:integer> "=>" <block:Value> => {
        Op::UnpackValueList(UnpackValueListOp {
            arity: arity.to_usize().unwrap(),
            value,
            block,
        })
    },

    "if_bool" <value:Value> <tru:Value> <fal:Value> <or:Value?> => {
        Op::IfBool(IfBoolOp {
            value,
            tru,
            fal,
            or,
        })
    },

    "trace_capture_raw" <then:Value> => {
        Op::TraceCaptureRaw(TraceCaptureRawOp {
            then,
        })
    },

    "match" <value:Value> "{" <entries:MatchEntry*> "}" => {
        Op::Match(MatchOp {
            value,
            entries,
        })
    },

    "case" <value:Value> "{" <entries:CaseEntry*> <no_match:CaseNoMatch?> "}" => {
        Op::Case(CaseOp {
            value,
            entries,
            no_match,
        })
    },

    // Unreachable
    "unreachable" => Op::Unreachable,

};

CaseEntry: CaseEntry = {
    "<" <patterns:Comma<CasePattern>> ">" "guard" <guard: Value> "=>" <target:Value> "(" <args:Comma<ident>> ")" ";" => {
        CaseEntry {
            patterns,
            args,
            guard,
            target,
        }
    },
    <pattern:CasePattern> "guard" <guard:Value> "=>" <target:Value> "(" <args:Comma<ident>> ")" ";" => {
        CaseEntry {
            patterns: vec![pattern],
            args,
            guard,
            target,
        }
    },
};

CaseNoMatch: Value = {
    "_" "=>" <Value> ";" => <>,
};

CasePattern: CasePattern = {
    "(" <CasePattern> ")" => <>,
    "{" <elements:Comma<CasePattern>> "}" => {
        CasePattern::Tuple {
            elements,
        }
    },
    "[" <mut heads:Comma<CasePattern>> <tail: ("|" <CasePattern>)?> "]" => {
        let mut acc = tail.unwrap_or(CasePattern::Value(Value::Nil));
        for elem in heads.drain(..).rev() {
            acc = CasePattern::ListCell {
                head: Box::new(elem),
                tail: Box::new(acc),
            };
        }
        acc
    },
    <name:ident> "@" <pat:CasePattern> => {
        CasePattern::Binding {
            name,
            pattern: Box::new(pat),
        }
    },
    "_" => {
        CasePattern::Wildcard
    }
};

MatchEntry: MatchEntry = {
    <kind:MatchKind> "=>" <target:Value> ";" => {
        MatchEntry {
            target,
            kind,
        }
    }
};

MatchKind: MatchKind = {
    "value" <value:Value> =>
        MatchKind::Value(value),
    "type" "%{" "}" =>
        MatchKind::Type(BasicType::Map),
    "{" "}" "arity" <arity:integer> =>
        MatchKind::Tuple(arity.to_usize().unwrap()),
    "[" "]" =>
        MatchKind::ListCell,
    "%{" <key:Value> "}" =>
        MatchKind::MapItem(key),
    "_" =>
        MatchKind::Wildcard,
};

Value: Value = {
    <m:Value> ":" <f:Value> "/" <a:Value100> =>
        Value::CaptureFunction(Box::new(m), Box::new(f), Box::new(a)),
    <left:Value> <op:BinOp> <right:Value100> =>
        Value::BinOp(Box::new(left), op, Box::new(right)),
    Value100,
};

Value100: Value = {
    ValueMax
};

ValueMax: Value = {
    "[" <head:Comma<Value>> <tail:("|" <Value>)?> "]" => {
        if head.len() == 0 {
            if let Some(tail) = tail {
                tail
            } else {
                Value::Nil
            }
        } else {
            Value::List(head, tail.map(Box::new))
        }
    },
    "{" <Comma<Value>> "}" =>
        Value::Tuple(<>),
    "<" <Comma<Value>> ">" =>
        Value::ValueList(<>),
    <atom> => Value::Atom(<>),
    <integer> => Value::Integer(<>),
    <Block> => Value::Block(<>),
    <variable> => Value::Value(<>),
};

BinOp: BinOp = {
    "==" => BinOp::Equal,
};

Block: Ident = {
    <ident> => <>,
    <l:@L> "type" <r:@R> =>
        Ident::new(Symbol::intern("type"), ByteSpan::new(l, r)),
};

extern {
    type Location = ByteIndex;
    type Error = ParserError;

    enum Token {
        "EOF" => Token::EOF,

        ident => Token::Ident(<Ident>),
        variable => Token::Variable(<Ident>),

        atom => Token::Atom(<Ident>),
        integer => Token::Integer(<Integer>),
        float => Token::Float(<Ident>),

        "(" => Token::ParenOpen,
        ")" => Token::ParenClose,
        "{" => Token::CurlyOpen,
        "}" => Token::CurlyClose,
        "[" => Token::SquareOpen,
        "]" => Token::SquareClose,
        "<" => Token::Less,
        ">" => Token::Greater,
        "%" => Token::Percent,
        "%{" => Token::MapOpen,
        ":" => Token::Colon,
        ";" => Token::Semicolon,
        "," => Token::Comma,
        "?" => Token::Question,
        "/" => Token::ForwardSlash,
        "=" => Token::Equals,
        "==" => Token::EqualsEquals,
        "=>" => Token::FatArrow,
        "_" => Token::Underscore,
        "|" => Token::Pipe,
        "@" => Token::At,

        "unpack" => Token::UnpackValueList,
        "unreachable" => Token::Unreachable,
        "arity" => Token::Arity,
        "if_bool" => Token::IfBool,
        "trace_capture_raw" => Token::TraceCaptureRaw,
        "value" => Token::Value,
        "match" => Token::Match,
        "type" => Token::Type,
        "case" => Token::Case,
        "guard" => Token::Guard,
        "except" => Token::Except,
    }

}
